apply plugin: "java"
apply plugin: "idea"
apply plugin: 'distribution'

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/resource'
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.zzm.migration.main.Main'

        String libPath = 'lib'
        String libJars = new File(libPath).list().findAll {
            it.endsWith('.jar')
        }.collect { "$libPath/$it" }.join(' ')

        attributes 'Class-Path': '. ' + libJars
    }
}

distributions {
    main {
        baseName = project.name
        contents {
            from('lib') {
                include '*.jar'
                into 'lib'
            }
            from('src/main/resource') {
                include '*.yml'
                into '.'
            }

            from { configurations.runtime.allArtifacts.files }

            from(projectDir) {
                include 'README.md'
            }
        }
    }
}

